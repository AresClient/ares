buildscript {
    repositories {
        jcenter()
        maven {
            name = "Fabric"
            url = "https://maven.fabricmc.net/"
        }
        maven {
            name = "Jitpack"
            url = "https://jitpack.io/"
        }
    }
    dependencies {
        classpath "com.github.Chocohead:fabric-loom:${project.chocoLoomVersion}" // SinÂ² Edition Loom
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
apply plugin: "fabric-loom"
apply plugin: "com.github.johnrengelman.shadow"

version = project.aresFabricVersion

dependencies {
    compile project(":ares-core")
    shadow (project(":ares-core")) {
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
        exclude group: "com.mojang", module: "brigadier"
        exclude group: "com.google.guava", module: "guava"
    }

    // to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.fabricMinecraftVersion}"
    mappings "net.fabricmc:yarn:${project.yarnVersion}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.fabricLoaderVersion}"

    include "net.fabricmc.fabric-api:fabric-resource-loader-v0:0.3.1+facf3bbf95"

    compile "com.github.Vatuu:discord-rpc:1.6.2"
    shadow "com.github.Vatuu:discord-rpc:1.6.2"

    implementation "com.gitlab.CDAGaming:fabritone:fabritone~1.16.x-Fabric-SNAPSHOT"
    include "com.gitlab.CDAGaming:fabritone:fabritone~1.16.x-Fabric-SNAPSHOT"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
task customShadowJar(type: ShadowJar) {
    configurations = [project.configurations.shadow]
    from remapJar.getArchivePath()
    exclude("mappings/**")
    exclude("LICENSE")
    setClassifier("release")
    minimize()
}

jar {
    manifest {
        attributes("Main-Class": "dev.tigr.ares.fabric.Main")
    }
}

build.dependsOn(":ares-fabric:customShadowJar")