buildscript {
    repositories {
        jcenter()
        maven {
            name = "Fabric"
            url = "https://maven.fabricmc.net/"
        }
        maven {
            name = "Jitpack"
            url = "https://jitpack.io/"
        }
    }
    dependencies {
        // use fork with subproject bug fix, waiting for this pr to be accepted
        classpath "com.github.solonovamax:fabric-loom:ef19233"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
apply plugin: "fabric-loom"
apply plugin: "com.github.johnrengelman.shadow"

version = project.aresFabricVersion

dependencies {
    // fabric deps
    minecraft "com.mojang:minecraft:${project.fabricMinecraftVersion}"
    mappings "net.fabricmc:yarn:${project.yarnVersion}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.fabricLoaderVersion}"

    // ares core
    implementation project(":ares-core")
    shadow(project(":ares-core")) {
        exclude group: "org.apache.logging.log4j", module: "log4j-api"
        exclude group: "com.mojang", module: "brigadier"
        exclude group: "com.google.guava", module: "guava"
    }

    // ares installer
    implementation(project(":ares-installer"))
    shadow(project(":ares-installer"))

    // include ares core deps
    include "net.fabricmc.fabric-api:fabric-resource-loader-v0:0.3.1+facf3bbf95"
    shadow "com.github.Vatuu:discord-rpc:1.6.2"

    // fabritone
    implementation "com.gitlab.CDAGaming:fabritone:fabritone~1.16.x-Fabric-SNAPSHOT"
    include "com.gitlab.CDAGaming:fabritone:fabritone~1.16.x-Fabric-SNAPSHOT"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
task customShadowJar(type: ShadowJar) {
    configurations = [project.configurations.shadow]
    from remapJar.getArchivePath()
    exclude("mappings/**")
    exclude("LICENSE")
    setClassifier("release")

    manifest {
        attributes("Main-Class": "dev.tigr.ares.installer.Installer")
    }
}

build.dependsOn(":ares-fabric:customShadowJar")